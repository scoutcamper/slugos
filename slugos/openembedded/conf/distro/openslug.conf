#@TYPE: Distribution
#@NAME: OpenSlug
#@DESCRIPTION: OpenSlug Linux Distribution for the NSLU2

#----------------------------------------------------------------------------------
# STANDARD OpenSlug DEFINITIONS
#----------------------------------------------------------------------------------
DISTRO_NAME = "OpenSlug"
# DISTRO_VERSION = "${SLUGOS_VERSION}${DISTRO_REVISION}-${DISTRO_TYPE}"
# DISTRO_FEED = "unstable"
DISTRO_TYPE = "beta"
SLUGOS_IMAGENAME = "openslug"
SLUGOS_IMAGESEX  = "big-endian"

# openslug builds a complete image (not just the parts)
SLUGOS_FLASH_IMAGE = "yes"

# NOTE: to build new packages set OPENSLUG_EXTRA_BBFILES to the full path name to
# the .bb files for the packages to build - see ucslugc-packages.conf in this
# directory
OPENSLUG_EXTRA_BBFILES ?= ""

#----------------------------------------------------------------------------------
# FIRMWARE CONFIGURATION
#----------------------------------------------------------------------------------
# EXTRA PACKAGES
# --------------
# Filesystem support modules are listed in here for easy removal,
# as is the lrzsz serial package and other kernel modules which are
# not absolutely required for boot.
# NOTE: only jffs2 support is an absolute requirement of boot, even
# the ext2/ext3 support is optional!
OPENSLUG_EXT2_PROGS =  "e2fsprogs-mke2fs e2fsprogs-fsck e2fsprogs-e2fsck"
OPENSLUG_REISER_PROGS = "reiserfsprogs"

# The standard firmware contents and additional packages built as requirements
# of the firmware are defined here in OPENSLUG_STANDARD_RDEPENDS and
# OPENSLUG_STANDARD_DEPENDS respectively.
OPENSLUG_STANDARD_RDEPENDS = ""

# Support for a variety of file systems is built in to the openslug
# image, programs to format disks with these file systems may be
# added or removed.  By default ext2. ext3 and reiserfs supported
# is included by the following lines.
# 
# These lines add support for formatting ext2 and ext3 file systems
# on a hard disk attached to the NSLU2.  ext3 is the standard Linux
# file system.
OPENSLUG_STANDARD_RDEPENDS += "${OPENSLUG_EXT2_PROGS}"

# These lines add support for formatting reiserfs file systems on
# a hard disk.
OPENSLUG_STANDARD_RDEPENDS += "${OPENSLUG_REISER_PROGS}"

# These lines add support for an X/Y/ZModem package called lrzsz
# (this is of use for people with modified NSLU2 hardware which
# supports a serial port.)
OPENSLUG_STANDARD_RDEPENDS += "lrzsz"

# Filesystem selection.  Adding entries here adds the module to the
# image.  The module must be built as part of nslu2-kernel (i.e. it
# must be specified as a module in the defconfig file).  The NLS
# support charset modules must be given explicitly and must match
# the codepage/iocharset and NLS handling for the file systems which
# require them.  The installed lanugage set is minimal but sufficient
# for any file system (since it uses utf8).  See
# http://www.nslu2-linux.orgwiki/HowTo/MountFATFileSystems
# for more information on the language behaviour of the DOS file
# systems.
#
# KERNEL LEVEL FILE SYSTEM SUPPORT
# --------------------------------
# NOTE: removing kernel-module-nfs from this list will prevent NFS
# boot (however you can do a simple flash file system boot - no
# attached disk - and install the nfs modules from ssh.)
OPENSLUG_STANDARD_RDEPENDS += "\
kernel-module-ext2 \
kernel-module-jbd \
kernel-module-ext3 \
kernel-module-nfs \
kernel-module-vfat \
kernel-module-ntfs \
kernel-module-isofs \
kernel-module-udf \
kernel-module-reiserfs \
kernel-module-nls-cp437 \
kernel-module-nls-utf8 \
"

# This documents other file systems which are built but not installed
# by default in the flash image.
#
# kernel-module-msdos - mounts a FAT file system without long file names
#                       (use kernel-module-vfat, it shows the long names
#                       if present.)

# The 'standard' values are the defaults for the 'extra' packages added
# to the firmware image.  None of these things are requirements to have
# a bootable, useable, system however they *are* expected to be present
# in OpenSlug - see nslu2-bag for a more minimal configuration and some
# more instructions about how to make a non-openslug image.
OPENSLUG_EXTRA_RDEPENDS ?= ""
SLUGOS_EXTRA_RDEPENDS = "${OPENSLUG_STANDARD_RDEPENDS} ${OPENSLUG_EXTRA_RDEPENDS}"

#----------------------------------------------------------------------------------
# PACKAGE VERSION CONTROL
#----------------------------------------------------------------------------------
PREFERRED_VERSION_nslu2-linksys-firmware ?= "2.3r63"

#----------------------------------------------------------------------------------
# BASE VERSION SELECTION
#----------------------------------------------------------------------------------
# b - big-endian
# a - ARM instruction set
# g - glibc
#
include conf/distro/slugos-bag.conf
