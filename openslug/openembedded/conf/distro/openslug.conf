#@TYPE: Distribution
#@NAME: OpenSlug
#@DESCRIPTION: OpenSlug Linux Distribution for the NSLU2

DISTRO_NAME = "OpenSlug"
DISTRO_VERSION = "1.12-beta"
DISTRO_TYPE ?= "beta"

TARGET_FPU_local ?= "soft"
TARGET_FPU := "${TARGET_FPU_local}"
TARGET_OS_local ?= "linux"
TARGET_OS := "${TARGET_OS_local}"

USE_NLS ?= "no"
USE_NLS_glib-2.0 = "yes"

INHERIT += " package_ipk debian nslu2_flashimg"

IMAGE_FSTYPES = "jffs2"

OPENSLUG_DEVICE_TABLE = "${@bb.which(bb.data.getVar('BBPATH', d, 1), 'files/device_table-openslug.txt')}"
EXTRA_IMAGECMD_jffs2 = "--pad --big-endian --eraseblock=0x20000 -D ${OPENSLUG_DEVICE_TABLE}"

OPENSLUG_EXTRA_DEPENDS  ?= ""
OPENSLUG_EXTRA_RDEPENDS ?= "\
kernel-module-usbnet \
kernel-module-pegasus kernel-module-p8022 kernel-module-kaweth \
kernel-module-usbserial \
kernel-module-pl2303 kernel-module-ftdi-sio"

# Filesystem selection: set in local.conf to override.  Adding entries
# here adds the module to the image.  The module must be built as part
# of openslug-kernel (i.e. it must be specified as a module in the
# defconfig file).  The NLS support charset modules must be given
# explicitly and must match the codepage/iocharset and NLS handling
# for the file systems which require them.  The installed lanugage
# set is minimal but sufficient for any file system (since it uses
# utf8).  See http://www.nslu2-linux.orgwiki/HowTo/MountFATFileSystems
# for more information on the language behaviour of the DOS file
# systems.
#
# NOTE: removing kernel-module-nfs from this list will prevent NFS
# boot (however you can do a simple flash file system boot - no
# attached disk - and install the nfs modules from ssh.)
OPENSLUG_EXTRA_FILESYSTEMS ?= "\
kernel-module-nfs \
kernel-module-vfat \
kernel-module-ntfs \
kernel-module-isofs \
kernel-module-udf \
kernel-module-nls-cp437 \
kernel-module-nls-utf8 \
"

# This documents other file systems which are built but not installed
# by default in the flash image.
#
# kernel-module-msdos - mounts a FAT file system without long file names
#                       (use kernel-module-vfat, it shows the long names
#                       if present.)
# kernel-module-reiserfs - support for the reiser file system

OPENSLUG_EXTRA_INSTALL ?= "${OPENSLUG_EXTRA_RDEPENDS} ${OPENSLUG_EXTRA_FILESYSTEMS}"

# These lines are for backwards compatibility and will be removed soon.
BOOTSTRAP_EXTRA_DEPENDS  += "${OPENSLUG_EXTRA_DEPENDS}"
BOOTSTRAP_EXTRA_RDEPENDS += "${OPENSLUG_EXTRA_RDEPENDS} ${OPENSLUG_EXTRA_FILESYSTEMS}"

PREFERRED_PROVIDER_virtual/kernel ?= "openslug-kernel"
PREFERRED_PROVIDERS += "virtual/${TARGET_PREFIX}depmod:module-init-tools-cross"

PREFERRED_VERSION_openslug-kernel ?= "2.6.11.2"
PREFERRED_VERSION_ixp4xx-csr ?= "1.4"
PREFERRED_VERSION_ixp425-eth ?= "1.1"
