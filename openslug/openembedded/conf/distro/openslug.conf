#@TYPE: Distribution
#@NAME: OpenSlug
#@DESCRIPTION: OpenSlug Linux Distribution for the NSLU2

DISTRO_NAME = "OpenSlug"
DISTRO_VERSION = "2.3-beta"
DISTRO_TYPE ?= "beta"

# Add to the user's feeds from local.conf (there may be none)
FEED_URIS_append_linux += "cross##http://ipkg.nslu2-linux.org/feeds/openslug/cross/${DISTRO_VERSION}"
FEED_URIS_append_linux += "native##http://ipkg.nslu2-linux.org/feeds/openslug/native/${DISTRO_VERSION}"

#
# Uncomment the following to get the unstable feeds
#FEED_URIS_append_linux += "unstable_cross##http://ipkg.nslu2-linux.org/feeds/openslug/cross/unstable"
#FEED_URIS_append_linux += "unstable_native##http://ipkg.nslu2-linux.org/feeds/openslug/native/unstable"


TARGET_FPU_local ?= "soft"
TARGET_FPU := "${TARGET_FPU_local}"
TARGET_OS_local ?= "linux"
TARGET_OS := "${TARGET_OS_local}"

USE_NLS ?= "no"
USE_NLS_glib-2.0 = "yes"

INHERIT += " package_ipk debian nslu2_flashimg"

IMAGE_FSTYPES = "jffs2"

OPENSLUG_DEVICE_TABLE = "${@bb.which(bb.data.getVar('BBPATH', d, 1), 'files/device_table-openslug.txt')}"
EXTRA_IMAGECMD_jffs2 = "--pad --big-endian --eraseblock=0x20000 -D ${OPENSLUG_DEVICE_TABLE}"

OPENSLUG_EXTRA_DEPENDS  ?= "e2fsprogs reiserfsprogs upslug-native lrzsz"

# CMDLINE* macros define the kernel command line.  CMDLINE_DEBUG is provided
# to enable/disable debugging in the distro.  Here noirqdebug is used to
# remove the messages about unhandled interrupts - people debugging interrupt
# issues in the kernel need to cancel this (set CMDLINE_DEBUG empty in local.conf)
CMDLINE_DEBUG = "noirqdebug"

# Filesystem support modules are listed in here for easy removal,
# as is the lrzsz serial package and other kernel modules which are
# not absolutely required for boot.
# NOTE: only jffs2 support is an absolute requirement of boot, even
# the ext2/ext3 support is optional!
OPENSLUG_EXT2_PROGS =  "e2fsprogs-mke2fs e2fsprogs-fsck e2fsprogs-e2fsck"
OPENSLUG_REISER_PROGS = "reiserfsprogs"
OPENSLUG_EXTRA_RDEPENDS ?= "\
lrzsz \
${OPENSLUG_EXT2_PROGS} ${OPENSLUG_REISER_PROGS} \
kernel-module-usbnet \
kernel-module-pegasus kernel-module-p8022 kernel-module-kaweth \
kernel-module-usbserial \
kernel-module-pl2303 kernel-module-ftdi-sio"

# Filesystem selection: set in local.conf to override.  Adding entries
# here adds the module to the image.  The module must be built as part
# of openslug-kernel (i.e. it must be specified as a module in the
# defconfig file).  The NLS support charset modules must be given
# explicitly and must match the codepage/iocharset and NLS handling
# for the file systems which require them.  The installed lanugage
# set is minimal but sufficient for any file system (since it uses
# utf8).  See http://www.nslu2-linux.orgwiki/HowTo/MountFATFileSystems
# for more information on the language behaviour of the DOS file
# systems.
#
# NOTE: removing kernel-module-nfs from this list will prevent NFS
# boot (however you can do a simple flash file system boot - no
# attached disk - and install the nfs modules from ssh.)
OPENSLUG_EXTRA_FILESYSTEMS ?= "\
kernel-module-ext2 \
kernel-module-jbd \
kernel-module-ext3 \
kernel-module-nfs \
kernel-module-vfat \
kernel-module-ntfs \
kernel-module-isofs \
kernel-module-udf \
kernel-module-reiserfs \
kernel-module-nls-cp437 \
kernel-module-nls-utf8 \
"

# This documents other file systems which are built but not installed
# by default in the flash image.
#
# kernel-module-msdos - mounts a FAT file system without long file names
#                       (use kernel-module-vfat, it shows the long names
#                       if present.)

OPENSLUG_EXTRA_INSTALL ?= "${OPENSLUG_EXTRA_RDEPENDS} ${OPENSLUG_EXTRA_FILESYSTEMS}"

# Select the correct versions of the compiler tools (note that there
# is also some selection in machine/nslu2.conf)
PREFERRED_PROVIDERS += " virtual/${TARGET_PREFIX}gcc-initial:gcc-cross-initial"
PREFERRED_PROVIDERS += " virtual/${TARGET_PREFIX}gcc:gcc-cross"
PREFERRED_PROVIDERS += " virtual/${TARGET_PREFIX}g++:gcc-cross"

# Select the correct versions of the kernel and modules
PREFERRED_PROVIDER_virtual/kernel ?= "openslug-kernel"
PREFERRED_PROVIDERS += "virtual/${TARGET_PREFIX}depmod:module-init-tools-cross"

# Tracking the latest version, so the following is commented out.
# Uncomment it before release!
#PREFERRED_VERSION_openslug-kernel ?= "2.6.12.2"

PREFERRED_VERSION_ixp4xx-csr ?= "1.4"
PREFERRED_VERSION_ixp425-eth ?= "1.1"
